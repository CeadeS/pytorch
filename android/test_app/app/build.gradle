apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId "org.pytorch.testapp"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters ABI_FILTERS.split(",")
        }
        buildConfigField ("String", "MODULE_ASSET_NAME", "\"mobilenet2q.pt\"")
        buildConfigField ("String", "LOGCAT_TAG", "@string/app_name")
        buildConfigField ("long[]", "INPUT_TENSOR_SHAPE", "new long[]{1, 3, 228, 228}")

        addManifestPlaceholders([APP_NAME: "@string/app_name"])
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
    }
    flavorDimensions "model", "build"
    productFlavors {
        mobNet2Quant {
            dimension "model"
            applicationIdSuffix ".mobNet2Quant"
            buildConfigField ("String", "MODULE_ASSET_NAME", "\"mobilenet2q.pt\"")
            addManifestPlaceholders([APP_NAME: "PyMobNet2Quant"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-mobnet2q\"")
        }
        resnet18 {
            dimension "model"
            applicationIdSuffix ".resneti18"
            buildConfigField ("String", "MODULE_ASSET_NAME", "\"resnet18.pt\"")
            addManifestPlaceholders([APP_NAME: "PyResNet18"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-resnet18\"")
        }
        local {
            dimension "build"
        }
        nightly {
            dimension "build"
        }
    }
    packagingOptions {
        pickFirst '**/libfbjni.so'
        doNotStrip '**.so'
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'

    localImplementation project(':pytorch_android')
    localImplementation project(':pytorch_android_torchvision')
    nightlyImplementation 'org.pytorch:pytorch_android:1.4.0-SNAPSHOT'
    nightlyImplementation 'org.pytorch:pytorch_android_torchvision:1.4.0-SNAPSHOT'
}
